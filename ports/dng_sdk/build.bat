@echo off
setlocal enabledelayedexpansion
rem
rem Build script for the current library.
rem
rem This script is designed to be invoked by `mpt.bat` using the command `mpt <library_name>`.
rem It relies on specific environment variables set by the `mpt` process to function correctly.
rem
rem Environment Variables Provided by `mpt` (in addition to system variables):
rem   ARCH          - Target architecture to build for. Valid values: `x64` or `x86`.
rem   PKG_NAME      - Name of the current library being built.
rem   PKG_VER       - Version of the current library being built.
rem   ROOT_DIR      - Root directory of the msvc-pkg project.
rem   SRC_DIR       - Source code directory of the current library.
rem   PREFIX        - **Actual installation path prefix** for the *current* library after successful build.
rem   PREFIX_PATH   - List of installation directory prefixes for third-party dependencies.
rem
rem   For each direct dependency `{Dependency}` of the current library:
rem     {Dependency}_PREFIX - Actual installation path of the dependency `{Dependency}`.
rem     {Dependency}_SRC - Source code directory of the dependency `{Dependency}`.
rem     {Dependency}_VER - Version of the dependency `{Dependency}`.

call "%ROOT_DIR%\compiler.bat" %ARCH%
set BUILD_DIR=%SRC_DIR%\dng_sdk\projects\win
set C_OPTS=-diagnostics:column -experimental:c11atomics -fp:precise -MD -nologo -openmp:llvm -utf-8
set C_DEFS=-DWIN32 -D_WIN32_WINNT=_WIN32_WINNT_WIN10 -D_CRT_DECLARE_NONSTDC_NAMES -D_CRT_SECURE_NO_DEPRECATE -D_CRT_SECURE_NO_WARNINGS -D_CRT_NONSTDC_NO_DEPRECATE -D_CRT_NONSTDC_NO_WARNINGS -D_USE_MATH_DEFINES -DNOMINMAX
set CL=-MP %C_OPTS% %C_DEFS%

call :clean_stage
call :build_stage
call :install_stage
call :clean_stage
goto :end

:clean_stage
echo "Cleaning %PKG_NAME% %PKG_VER%"
cd "%SRC_DIR%"
if exist "dng_sdk\projects\win\dng_validate\Validate Release" (
  rmdir /s /q "dng_sdk\projects\win\dng_validate\Validate Release"
)
if exist "dng_sdk\projects\win\%ARCH%" (
  rmdir /s /q "dng_sdk\projects\win\%ARCH%"
)
if exist "libjxl\client_projects\win\%ARCH%" rmdir /s /q "libjxl\client_projects\win\%ARCH%"
if exist "xmp\toolkit\public\libraries" rmdir /s /q "xmp\toolkit\public\libraries"
if exist "xmp\toolkit\XMPCore\build\CMake64Static_VC16\XMPCoreStatic.dir" (
  rmdir /s /q "xmp\toolkit\XMPCore\build\CMake64Static_VC16\XMPCoreStatic.dir"
)
if exist "xmp\toolkit\XMPFiles\build\CMake64Static_VC16\XMPFilesStatic.dir" (
  rmdir /s /q "xmp\toolkit\XMPFiles\build\CMake64Static_VC16\XMPFilesStatic.dir"
)
exit /b 0

:build_stage
echo "Building %PKG_NAME% %PKG_VER%"
cd "%BUILD_DIR%"
msbuild dng_validate.sln /p:Configuration="Validate Release"                   ^
  /p:Platform=%ARCH% /p:PlatformToolset=v143 /p:UseEnv=true                    ^
  /p:SkipUWP=true || exit 1
exit /b 0

:install_stage
echo "Installing %PKG_NAME% %PKG_VER%"
if not exist "%PREFIX%\bin" mkdir "%PREFIX%\bin"
cd "%BUILD_DIR%\dng_validate\Validate Release\%ARCH%" && (
  xcopy /Y /F /I *.exe "%PREFIX%\bin"
)
exit /b 0

:end
